---
layout: default
title: bdarnell.com
permalink: /
---
<style>
  body { background-image: none; }
  #glcanvas {
    position: absolute;
    top: 0;
    right: 0;
    z-index: -1000;
  }
  .btn-graphics {
    background-color: #ffffff7f;
  }
</style>

<div class="btn-group nav-item m-2" role="group">

  <button class="btn btn-graphics" onclick="setFragmentShader('blank');">
    Colors
  </button>

  <span style="height: 100%; width: 0.2rem"></span>

  <button class="btn btn-graphics" onclick="setFragmentShader('deepfractal');">
    Fractal
  </button>

  <span style="height: 100%; width: 0.2rem"></span>

  <button class="btn btn-graphics" onclick="setFragmentShader('raytracer');">
    Raytracer
  </button>

</div>

<canvas id="glcanvas"> 
  Your browser doesn't seem to support this canvas.
</canvas>

<script id="vertex-shader" type="x-shader/x-vertex">
  attribute vec4 a_position;
  varying vec2 v_coords;

  void main() {
    gl_Position = vec4(a_position.xy, 0.0, 1.0);
    v_coords = gl_Position.xy * 0.5 + 0.5;
  }
</script>

<script id="fragment-shader-blank" type="x-shader/x-fragment">
  #ifdef GL_ES
    precision mediump float;
  #endif

  vec3 mainImage(vec2 uv, float iTime) {
    return vec3(0.5 + 0.5*cos(iTime+uv.xyx+vec3(0,2,4)));
  }

  // wrapper around shadertoy code:
  varying vec2 v_coords;
  uniform float time;

  void main() {
    gl_FragColor = vec4(mainImage(v_coords, time), 1.0);
  }
</script>

<script id="fragment-shader-raytracer" type="x-shader/x-fragment">
  #ifdef GL_ES
    precision mediump float;
  #endif

  #define PI 3.14159265
  #define TAU 6.2831853
  #define EPSILON 0.00001
  #define MOD(a, b) (a - (b * floor(a/b)))

  float random(vec2 seed) {
    return fract(sin(dot(seed.xy, vec2(12.9898, 78.233))) * 43758.5453) * 1.0;
  }

  vec3 randomUnitVector(vec2 seed) {
    return 1.2 * (vec3(random(seed), random(seed + 1.0), random(seed + 2.0)) - 0.5);
  }

  struct ray
  {
    vec3 origin;
    vec3 direction;
  };
      
  #define NORMAL -1
  #define SOLID 0
  #define LAMBERTIAN 1
  #define METAL 2
      
  struct material
  {
    int type;
    vec3 albedo;
    float quality;
  };
      
  struct hit
  {
    float t;
    vec3 point;
    vec3 normal;
    material material;
  };
      
  struct sphere
  {
    vec3 center;
    float radius;
    material material;
  };

  hit hitSphere(sphere s, ray r, hit h)
  {
    float a, b, c, d, t, q;
    a = dot(r.direction, r.direction);
    b = 2.0 * dot(r.direction, r.origin - s.center);
    c = dot(r.origin - s.center, r.origin - s.center) - s.radius * s.radius;
    d = b * b - 4.0 * a * c;
    if (d > 0.0) {
      q = 1.0;//float(c < 0.0);
    t = -(b + sqrt(d) * q) / (2.0 * a);    
      if (t > 0.0 && (t < h.t || h.t < 0.0)) {
        h.t = t;
        h.point = r.origin + r.direction * h.t;
        h.normal = q * normalize(h.point - s.center);
        h.material = s.material;
        return h;
      }
    }
    return h;
  }

  #define WHITE				vec3(1.0, 1.0, 1.0)
  #define LIGHT_GREY	vec3(0.7, 0.7, 0.7)
  #define GREY				vec3(0.5, 0.5, 0.5)
  #define DARK_GREY		vec3(0.3, 0.3, 0.3)
  #define BLACK 			vec3(0.0, 0.0, 0.0)
  #define RED 				vec3(1.0, 0.1, 0.1)
  #define YELLOW			vec3(1.0, 0.8, 0.2)
  #define GREEN				vec3(0.1, 1.0, 0.3)
  #define BLUE				vec3(0.2, 0.3, 1.0)
  #define SKY_BLUE		vec3(0.5, 0.7, 1.0)
  #define DARK_BLUE		vec3(0.0, 0.0, 0.1)

  // defines the scene and its component objects
  hit hitScene(ray r, float iTime) {
    
    hit h = hit(-1.0, vec3(0, 0, 0), vec3(0, 0, 0), material(SOLID, BLACK, 0.0));
    
    sphere s1 = sphere(
      vec3(
        0.2 * (abs(MOD(iTime * 2.0, TAU) - PI) - PI * 0.5), 
        0.3 * abs(sin(iTime * 2.0)), 
        -1.0
      ), 
      0.5, 
      material(NORMAL, RED, 1.0)
    );
    h = hitSphere(s1, r, h);
    
    sphere s2 = sphere(
      vec3(0.0, -100.5, -1.0), 
      100.0,  
      material(METAL, GREY, 0.03)
    );
    h = hitSphere(s2, r, h);
    
    return h;
    
  }

  struct camera
  {
    vec3 origin;
    vec3 direction;
    vec3 vertical;
    vec3 horizontal;
  };

  camera buildCamera(vec3 location, vec3 target, vec3 up, float fov, float aspect) {
    vec3 u, v, w;
    float theta = radians(fov);
    float width = tan(theta / 2.0);
    float height = width * aspect;
    
    w = normalize(location - target);
    u = normalize(cross(up, w));
    v = cross(w, u);
    
    camera cam; // Set up camera parameters:
    cam.origin = location;
    cam.direction = -w;
    cam.vertical = 2.0 * height * v;
    cam.horizontal = 2.0 * width * u;
    return cam;
  }

  #define MOON_DIR normalize(vec3(1.0, -0.3, 1.0))
  #define MOON_RAD 0.04

  #define MAX_BOUNCES 3

  vec3 trace(ray r, vec2 seed, float iTime) 
  {
    vec3 color;
    
    vec3 factor = vec3(1.0);
    for (int bounce = 0; bounce < MAX_BOUNCES; bounce++) {

      hit h = hitScene(r, iTime);
      
      if (h.t > 0.0) { // if a hit was found, return the hit color 
        if (h.material.type == NORMAL) {
          color += factor * (h.normal / 2.0 + 0.5);
          factor *= 0.0;
        } else if (h.material.type == SOLID) {
          color += factor * h.material.albedo;
          factor *= 0.0;
        } else if (h.material.type == LAMBERTIAN) {
          vec3 target = normalize(h.normal + randomUnitVector(seed));
          r = ray(h.point, target);
          factor *= (h.material.albedo);
        } else if (h.material.type == METAL) {
          vec3 reflected = reflect(normalize(r.direction), h.normal);
          r = ray(h.point, normalize(reflected + h.material.quality * randomUnitVector(seed)));
          factor *= (h.material.albedo);
        }
      } else { // otherwise, return the sky color
        float elevation = 0.5 * (normalize(r.direction).y + 1.0);
        color += factor * (SKY_BLUE * elevation + DARK_BLUE * (1.0 - elevation));

        // the mooon
        vec3 toMoonCenter = normalize(r.direction) - MOON_DIR;
        if (length(toMoonCenter) < MOON_RAD) {
          color = LIGHT_GREY + (WHITE - LIGHT_GREY) * (MOON_RAD - length(toMoonCenter)) / MOON_RAD;
        }

        factor *= 0.0;
      }
      
    }
    
    return color;
    
  }

  // Warp constants:
  #define K_A 0.0005
  #define K_B 0.15
  #define K_C 10.0

  #define SAMPLES 3.0
  vec3 mainImage(vec2 fragCoord, vec2 iResolution, vec2 iMouse, float active, float iTime)
  {
    // add some visual warping around mouse
    if (active == 1.0) {
      float distFromMouse = length(iMouse - fragCoord);
      float warp = exp(- K_A * distFromMouse * distFromMouse) * sin(K_B * distFromMouse - K_C * iTime);
      fragCoord += warp * distFromMouse;
    }

    float scale = iResolution.y / iResolution.x;

    float dist = 2.0;
    float theta = TAU * (iMouse.x / iResolution.x) + 0.05 * iTime;
    float var = 5.0 * iMouse.y / iResolution.y;
    //camera cam = buildCamera(vec3(dist * cos(omega * iTime), dist, dist * sin(omega * iTime) - 1.0), vec3(0, 0, -1), vec3(0, 1, 0), 70.0, scale);
    
    camera cam = buildCamera(
      vec3((dist + var) * cos(theta), var, (dist + var) * sin(theta) - 1.0), // (2, 2, -1)
      vec3(0, 0, -1), 
      vec3(0, 1, 0), 
      70.0, 
      scale
    );
    
    vec3 color;
    
    // Loop through each sample in the pixel
    for (float dx = 0.0; dx < 1.0; dx += 1.0 / (SAMPLES - EPSILON)) {
      for (float dy = 0.0; dy < 1.0; dy += 1.0 / (SAMPLES - EPSILON)) {
        
        vec2 delta = vec2(dx, dy);
        vec2 uv = ((fragCoord + delta + 1.0 / SAMPLES * random(fragCoord + delta)) / iResolution.xy) - 0.5;
        ray r = ray(cam.origin, cam.direction + uv.x * cam.horizontal + uv.y * cam.vertical);
        color += trace(r, uv, iTime);
        
      }
    }
    
    color = color / (SAMPLES * SAMPLES);
    return sqrt(color);
  }

  // wrapper around shadertoy code:
  varying vec2 v_coords;
  uniform float time;
  uniform float width;
  uniform float height;
  uniform float mouseX;
  uniform float mouseY;
  uniform float active; // boolean storing mouse click state

  void main() {
    vec2 iResolution = vec2(width, height);
    vec2 fragCoord = v_coords.xy * iResolution.xy;
    vec2 iMouse = vec2(mouseX, height - mouseY);
    
    gl_FragColor = vec4(mainImage(fragCoord, iResolution, iMouse, active, time), 1.0);
  }
  
</script>

<script id="fragment-shader-fractal" type="x-shader/x-fragment">
  #ifdef GL_ES
    precision highp float;
  #endif

  #define TAU 6.28318

  #define NUM_COLOR 50.0
  #define MAX_ITER 200
  
  int mandlebrot( vec2 c ) {
    int i = 0;
    vec2 z = vec2(0);
      
      for (int i = 0; i < MAX_ITER; i++) {
          z = vec2(z.x*z.x-z.y*z.y, 2.0*z.x*z.y) + c;
          if (length(z) >= 2.0) { return i; }
      }
      
      return -1;
      
  }
  
  #define RAINBOW 0
  #define COVFEFE 1
  #define ZEBRA 2
  
  vec3 color( int scheme, float x ) {
    if (scheme == RAINBOW) {
      return sqrt((sin(TAU*(x+vec3(0,1,2)/3.))+1.0)/2.0);
    }
    if (scheme == COVFEFE) {
      return 0.5 + 0.5*cos(2.7+x*TAU + vec3(0.0,.6,1.0));
    }
    if (scheme == ZEBRA) {
      return vec3(1);
    }
  }
  
  vec3 simulate( vec2 coord ) {
    int iterations = mandlebrot(coord);
    if (iterations < 0) {
      return vec3(0);
    } else {
      return color(RAINBOW, mod(float(iterations), NUM_COLOR) / NUM_COLOR);
    }
  }
  
  // Frame Coordinates: (zoom, lowerLeft, upperRight, origin)
  #define ZR 1.25
  #define LL vec2(-2.0, -1.0)
  #define UR vec2(1.0, 1.0)
  #define OR vec2(0.001643721971153, 0.822467633298876)
  #define RES vec2(800, 500)
  
  vec2 map( vec2 fragCoord, vec2 resolution, float time ) {
    vec2 lowerLeft = (LL-OR)*pow(ZR,-time)+OR;
    vec2 upperRight = (UR-OR)*pow(ZR,-time)+OR;
    return lowerLeft + (upperRight - lowerLeft) * fragCoord/RES;
  }

  varying vec2 v_coords;
  uniform float time;
  uniform float width;
  uniform float height;

  void main() {
    vec2 resolution = vec2(width, height);
    vec2 fragCoord = v_coords * resolution;
    
    gl_FragColor = vec4(
      (simulate(map(fragCoord, resolution, time)) + 
      simulate(map(fragCoord + vec2(0, 0.5), resolution, time)) + 
      simulate(map(fragCoord + vec2(0.5, 0), resolution, time)) + 
      simulate(map(fragCoord + vec2(0.5, 0.5), resolution, time))) / 4.0, 1.0);
    //gl_FragColor = vec4(simulate(map(fragCoord, resolution, time)), 1.0);
  }
  
</script>

<script id="fragment-shader-deepfractal" type="x-shader/x-fragment">
  #ifdef GL_ES
    precision highp float;
  #endif

  #define TAU 6.28318
  #define NUM_COLOR 50.0
  #define MAX_ITER 800
  #define ZOOM_RATE 1.5
  #define ORIGIN vec2(-0.05026782, 0.6721055)
  #define ORIGIN_SMALL vec2(1e-16, -1.5e-19)

  int mandelbrot( vec4 c ) {
    vec4 z = vec4(0);
    for (int i = 0; i < MAX_ITER; i++) {
      z.zw = vec2(z.z*z.z - z.w*z.w, z.z*z.w*2.0) 
          + 2.0 * vec2(z.x*z.z - z.y*z.w, z.x*z.w + z.y*z.z);
      z.xy = vec2(z.x*z.x - z.y*z.y, z.x*z.y*2.0);
      z += c;
      if (length(z) >= 4.0) { return i; }
      // Hack to allow for deeper zoom: https://www.shadertoy.com/view/3l2Xz3
      if (length(z.zw) / length(z.xy) > 0.01) {
        z.xy = z.xy + z.zw;
        z.zw = vec2(0.0);
      }
    }
    return -1;
  }

  vec3 color( int i ) {
    return 0.5 + 0.5*cos(2.7+(mod(float(i), NUM_COLOR) / NUM_COLOR)*TAU + vec3(0.0,.6,1.0));
  }

  varying vec2 v_coords;
  uniform float time;
  uniform float width;
  uniform float height;

  void main()
  {
    vec2 resolution = vec2(width, height);
    vec2 fragCoord = v_coords * resolution;
    vec2 coord = (2.0*fragCoord - resolution) * pow(ZOOM_RATE, -time) / resolution.y;
    int value = mandelbrot(vec4(ORIGIN, coord + ORIGIN_SMALL));
    gl_FragColor = vec4(color(value) * float(value >= 0), 1.0);
  }

</script>

<script src="/assets/js/graphics.js"></script>

